1)
MATCH (p:Paper)<-[c:CITED_BY]-(a:Paper)-[r:PUBLISHED_AT]->(d:Document)
WHERE d.DocumentType ="Conference"
WITH d.ConferenceName AS name, a, count(c) AS citations
ORDER BY name, citations DESC
WITH name, collect({paper: a.Title, cited: citations}) AS papers
RETURN name AS Conference, [p IN papers[..3] | p.paper] AS Papers, [p IN papers[..3] | p.cited] AS Cited

2)
MATCH (a:Author)<-[w:WRITTEN_BY]-(p:Paper)-[r:PUBLISHED_AT]->(d:Document)
WITH a,d.ConferenceName as Conference_Name, count(DISTINCT d.Volume) as Editions
WHERE Editions > 3
RETURN Conference_Name, collect(a.AuthorName)  as Community_member

3)
MATCH (d:Document)<-[w:PUBLISHED_AT]-(p:Paper)-[b:CITED_BY]->(p1:Paper)
WHERE d.DocumentType = 'Journal'
WITH d.Title as Journal, toInteger(b.Year) as Citation_Year, count(b) AS Citations
WITH Journal, Citation_Year, Citations, toInteger(Citation_Year)-1 as first, toInteger(Citation_Year)-2 as second
MATCH (d2:Document)<-[z:PUBLISHED_AT]-(p2:Paper)
WHERE d2.Title = Journal AND toInteger(p2.Year) IN [first, second]
WITH Journal, Citation_Year,first, second, Citations, count(DISTINCT d2) AS Publications
RETURN Journal, Citation_Year, (ToFloat(Citations)/ToFloat(Publications) )as Impact_factor
ORDER BY Citation_Year DESC

4)
MATCH (a:Author)<-[w:WRITTEN_BY]-(p:Paper)-[b:CITED_BY]->(p2:Paper) WITH a, p, count(b) AS citations
WITH a, p, citations ORDER BY citations DESC
WITH a, count(p) AS total, collect(citations) AS list
WITH a, total, list, [x in range(1, size(list)) WHERE x <= list[x - 1] | [list[x - 1], x] ] AS list_hindex
WITH *, list_hindex[-1][1] AS h_index
ORDER BY h_index DESC
RETURN a.AuthorName as Author, h_index
